fn basic(lexes){
    mut turi_code = {
        for each lex in lexes list {
            if lex.atLineStart {
                continue "\nmut" when lex.text == "LET";
                continue "\nwhile" when lex.text == "WHILE";
                continue "\n}" when lex.text == "WEND";
                continue "\nif" when lex.text == "IF";
                continue "\n}" when lex.text == "ENDIF";
                continue "\nprintln" when lex.text == "PRINT";
            }
            continue "{\n" when lex.text == "DO";
            continue "} else {\n" when lex.text == "ELSE";
            continue "{\n" when lex.text == "THEN";
            
            lex;
        }
    }
    turi_code
}



#basic

fn LIST(lexes){
    try {
        for each lex in lexes list {
            if lex.atLineStart {
                println "";
            }
            print lex.text, " "
            lex
        }
        }finally {
        println;
    }
}

// #LIST // uncomment at the start of the line to print out the program

LET A = 12
LET B = 14
IF A+B == 26 THEN
PRINT "Yes, it works."
ELSE
PRINT "No, it does not work, it is ",(A+B)
ENDIF

LET I = 0
WHILE I < 10 DO
PRINT I
I = I+1
WEND

